fpath=(~/bin/comps $fpath)

zstyle :compinstall filename '/home/hexular/.zshrc'

autoload -Uz colors && colors
autoload -Uz compinit && compinit
autoload -Uz bashcompinit && bashcompinit

setopt interactive_comments

export HISTSIZE=500000
export HISTFILESIZE=500000
export SAVEHIST=500000
HISTFILE="${HISTFILE:-/home/hexular/.zsh_history}"
toggle-history() {
    if [ "/dev/null" = "$HISTFILE" ]; then
        unset HISTFILE # remove environment variable
        HISTFILE=~/.zsh_history
        echo 'turned on history'
    else
        # hidden history should also work in nested shells
        export HISTFILE=/dev/null
        echo 'turned off history'
    fi
}
export MANPAGER='nvim +Man!'
export MANWIDTH=80
export EDITOR=nvim
export VISUAL=nvim
export GPG_TTY="$(tty)"

alias ls='ls --color=auto'
alias grep='grep --color=auto'
alias NR='awk '"'"'{ print NR " " $0 }'"'"
alias ip='ip -color=auto'
alias rs-clippy='cargo clippy --all-targets --all-features'
alias rs-fmt='cargo fmt'
alias ll='ls -lah'
alias pbimgview='pbimgpaste | feh -'
alias gs='git status'

setopt HIST_IGNORE_SPACE

# System specific config
HOSTNAME="$(hostname -s)"
case "$HOSTNAME" in
    hexagon)
        HOST_COLOR="red"
        alias pbcopy='xclip -selection c'
        alias pbpaste='xclip -selection c -o'
        alias pbimgpaste='xclip -selection c -t image/png -o'
        alias open='xdg-open'
        ;;
    hexamac)
        HOST_COLOR="green"
        if [ -e /Users/hexular/.nix-profile/etc/profile.d/nix.sh ]; then 
            source /Users/hexular/.nix-profile/etc/profile.d/nix.sh; 
        fi # added by Nix installer
        export NIX_PATH=$HOME/.nix-defexpr/channels${NIX_PATH:+:}$NIX_PATH
        ;;
esac

alias tree='eza --tree'

if [ ! -z "$HOST_COLOR" ]; then
    ITALIC="$(tput sitm)"
    RST="$(tput sgr0)"
    NL=$'\n'
    PROMPT="<%{%F{$HOST_COLOR}%}$HOSTNAME%{%f%}:%~>$NL%# "
    if [ ! -z "$IN_NIX_DEV_SHELL_NAME" ]; then
        nixprompt+=("@$IN_NIX_DEV_SHELL_NAME")
    fi
    if [ ! -z "$GOTTEN_NIX_PKGS" ]; then
        nixprompt+=("+($GOTTEN_NIX_PKGS)")
    fi
    if [ "${#nixprompt[@]}" != 0 ]; then
        PROMPT="%{$ITALIC%}$(printf '%s ' "${nixprompt[@]}")%{$RST%}$PROMPT"
    fi
else
    PROMPT="<%M:%~>%# "
fi

bindkey -v

function zle-keymap-select {
  if [[ ${KEYMAP} == vicmd ]] ||
     [[ $1 = 'block' ]]; then
    echo -ne '\e[1 q'

  elif [[ ${KEYMAP} == main ]] ||
       [[ ${KEYMAP} == viins ]] ||
       [[ ${KEYMAP} = '' ]] ||
       [[ $1 = 'beam' ]]; then
    echo -ne '\e[5 q'
      fi
}
zle -N zle-keymap-select

# Use beam shape cursor on startup.
_fix_cursor() {
   echo -ne '\e[5 q'
}
precmd_functions+=(_fix_cursor)

# reduce ESC timeout
KEYTIMEOUT=5

HM_SESSION="$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh"
if [ -e "$HM_SESSION" ]; then source "$HM_SESSION"; fi

edit-history() {
    tempHistFile="$(mktemp)"
    chmod 600 "$tempHistFile"
    echo "putting history in $tempHistFile"
    fc -W "$tempHistFile"
    if "$EDITOR" "$tempHistFile"; then
        fc -R /tmp/zsh_history
        echo "history successfully edited"
    else
        echo "non-zero exit code, history not edited"
    fi
}

# Just displays information about a video given in $1
videoinfo() { 
    for arg in "$@"; do
        echo "$arg:"
        ffprobe -i "$arg" 2>&1 | grep -Po '((?<=^  )Stream.*$)|(Duration: [^,]*)' | sed 's/^/\t/' 
    done
}

# Dumps the environment in a shell-friendly format.
dumpenv() {
    env -0 | awk '
        BEGIN { 
            RS="\0"; 
            apo="'"'"'"; 
        } 
        { 
            eq=index($0,"="); 
            left=substr($0,1,eq-1); 
            right=substr($0,eq+1); 
            gsub(/'"'"'/, apo + "\"" + apo + "\"" + apo, right); 
            print left "=" apo right apo; 
        }'
}

# Too slow, commenting out for now
# command_not_found_handler() {
#     IFS=$'\n' candidates=($(nix-locate -w "bin/$1" --minimal | sed '/^(/d'))
#     if [ "${#candidates[@]}" -eq 0 ]; then 
#         echo "zsh: command not found: $1"
#         exit 127
#     else
#         echo "the following packages contain the binary `$1`:"
#         select opt in "${candidates[@]}"; do
#             break
#         done
#         exec nix shell "nixpkgs#$opt"
#     fi
# }
